/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * The OpenSearch Contributors require contributions made to
 * this file be licensed under the Apache-2.0 license or a
 * compatible open source license.
 *
 * Modifications Copyright OpenSearch Contributors. See
 * GitHub history for details.
 */

buildscript {
    ext {
        opensearch_version = System.getProperty("opensearch.version", "3.3.0-SNAPSHOT")
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://ci.opensearch.org/ci/dbc/snapshots/maven/" }
        maven { url "https://ci.opensearch.org/ci/dbc/snapshots/lucene/" }
    }

    dependencies {
        classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'com.diffplug.spotless' version '6.25.0'
}

apply plugin: 'opensearch.opensearchplugin'
apply plugin: 'opensearch.yaml-rest-test'
apply plugin: 'opensearch.internal-cluster-test'
apply plugin: 'opensearch.java-agent'

ext {
    projectSubstitutions = [:]
    licenseFile = rootProject.file('LICENSE.txt')
    noticeFile = rootProject.file('NOTICE.txt')
}

opensearchplugin {
    name 'storage-encryption'
    description 'Encrypts and decrypts index data at rest.'
    classname 'org.opensearch.index.store.CryptoDirectoryPlugin'
    version opensearch_version
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://ci.opensearch.org/ci/dbc/snapshots/maven/" }
    maven { url "https://ci.opensearch.org/ci/dbc/snapshots/lucene/" }
}

dependencies {
    compileOnly "org.opensearch:opensearch:${opensearch_version}"
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${versions.log4j}"
    testImplementation "org.opensearch.test:framework:${opensearch_version}"
    testImplementation "commons-io:commons-io:2.13.0"

    // Integration test dependencies
    testImplementation "org.opensearch.plugin:reindex-client:${opensearch_version}"
    internalClusterTestImplementation "org.opensearch.plugin:crypto-kms:${opensearch_version}"

    implementation 'com.github.ben-manes.caffeine:caffeine:3.0.0'
    api "io.netty:netty-buffer:4.2.5.Final"
    api "io.netty:netty-transport:4.2.5.Final"
    api "io.netty:netty-transport-native-unix-common:4.2.5.Final"
    api "io.netty:netty-transport-classes-io_uring:4.2.5.Final"
    api "io.netty:netty-common:4.2.5.Final"
    api "io.netty:netty-transport-native-io_uring:4.2.5.Final"
}

configurations.all {
    resolutionStrategy {
        force 'com.google.errorprone:error_prone_annotations:2.41.0'
    }
}

test {
    useJUnit()
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
    systemProperty 'tests.security.manager', 'false'
    systemProperty 'tests.jvm.argline', '--enable-native-access=ALL-UNNAMED'
    jvmArgs += [
        '--enable-native-access=ALL-UNNAMED'
    ]
}

tasks.named('internalClusterTest').configure {
    systemProperty 'tests.security.manager', 'false'
    systemProperty 'java.security.policy', 'test.policy'
    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }
    jvmArgs += [
        '--enable-native-access=ALL-UNNAMED'
    ]
}

tasks.named('yamlRestTest').configure {
}

testClusters.yamlRestTest {
    setting 'plugins.crypto.enabled', 'true'
    setting 'node.store.crypto.pool_size_percentage', '0.0'
    setting 'node.store.crypto.warmup_percentage', '0.0'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:-deprecation'
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    options.encoding = 'UTF-8'
    options.release = 21
    options.compilerArgs << '-Xlint:-deprecation'
    options.compilerArgs += "--enable-preview"

    // Patch all compiled class files to remove preview flag requirement by java
    // this is like tricky java to not consider these files using preview.
    doLast {
        def patchedCount = 0
        destinationDirectory.getAsFileTree().matching(new PatternSet().include('**/*.class')).each { classFile ->
            new RandomAccessFile(classFile, 'rw').withCloseable { f ->
                int magic = f.readInt()
                if (magic != (int)0xCAFEBABE) return
                f.seek(4L)
                short minor = f.readShort()
                short major = f.readShort()
                if (major == 65 && minor == (short)0xFFFF) {
                    f.seek(4L)
                    f.writeShort(0)
                    patchedCount++
                }
            }
        }
        logger.lifecycle("Patched ${patchedCount} class files to remove preview flag")
    }
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.addStringOption('Xdoclint:all', '-quiet')
    options.addStringOption('tag', 'opensearch.internal:a:Internal:')
    options.addStringOption('source', '21')
    options.addStringOption('-enable-preview', '-quiet')
    failOnError = false
}

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

check.dependsOn jacocoTestReport

dependencyLicenses.enabled = false
thirdPartyAudit.enabled = false
loggerUsageCheck.enabled = false
validateNebulaPom.enabled = false
testingConventions.enabled = false
forbiddenApisMain.enabled = false
forbiddenApisTest.enabled = false

task allTests {
    dependsOn test, internalClusterTest, yamlRestTest
    group = 'Verification'
    description = 'Runs all tests (Unit, Integration, and YAML)'
}

spotless {
    java {
        removeUnusedImports()
        importOrder 'java', 'javax', 'org', 'com'
        licenseHeaderFile 'spotless.license.java'
        eclipse().configFile rootProject.file('.eclipseformat.xml')
    }
}
